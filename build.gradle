plugins {
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
    id "com.bmuschko.docker-remote-api" version "6.1.2"
}

apply from: "$rootDir/gradle/versioning.gradle"

group = 'ch.so.agi.ilivalidator'

description = """ilivalidator web service"""

sourceCompatibility = '8'


sourceSets {
    dockerTest {
        java.srcDir "$projectDir/src/dockerTest/java"
        //resources.srcDir "$projectDir/src/dockerTest/resources"    
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
    customFunctionsDeps
    customFunctionsDeps.transitive = false	
    
    dockerTestImplementation.extendsFrom testImplementation
    dockerTestRuntime.extendsFrom testRuntime    
}

repositories {
	jcenter()
	mavenCentral()
    maven { url "http://jars.interlis.ch/" }
    maven { url "http://repo.maven.apache.org/maven2" }	
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'commons-io:commons-io:2.6'	
    implementation 'ch.interlis:ilivalidator:1.11.3'
    implementation "org.jgrapht:jgrapht-core:1.3.1" // for custom functions
    implementation 'ch.interlis:ili2db:4.3.2' // for custom functions

    customFunctionsDeps "ch.so.agi.ilivalidator:ilivalidator-custom-functions:1.0.17"

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
    testImplementation 'org.jsoup:jsoup:1.12.1'	
    dockerTestCompile sourceSets.test.output          
}

test {
	useJUnitPlatform()
    failFast = true
    testLogging {
        showStandardStreams = true
    }	
}

task deleteFilesLibsExt(type: Delete) {
    delete fileTree('src/main/resources/libs-ext/') {
        include '**.jar'
    }
}

task copyToLibsExt(type: Copy) {
    from configurations.customFunctionsDeps
    into "src/main/resources/libs-ext/"
}
copyToLibsExt.dependsOn(deleteFilesLibsExt)
build.dependsOn(copyToLibsExt)
classes.dependsOn(copyToLibsExt)

task deleteFilesJarDir(type: Delete) {
    delete fileTree("${buildDir}/dependency/")
    delete file("${buildDir}/dependency/")
}

task unzipJar(type: Copy) {
    from zipTree(file("${buildDir}/libs/ilivalidator-web-service-websocket-"+version+".jar"))
    into file("${buildDir}/dependency/")
}
unzipJar.dependsOn(deleteFilesJarDir)
bootJar.finalizedBy(unzipJar)

task dockerTest(type: Test) {
    description = 'Runs the docker tests.'
    group = 'verification'
    
    useJUnitPlatform()
    testClassesDirs = sourceSets.dockerTest.output.classesDirs
    classpath = sourceSets.dockerTest.runtimeClasspath    
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn dockerTest

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerLogsContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer.ExposedPort
import java.text.SimpleDateFormat

docker {
    registryCredentials {
        username = System.env.DOCKER_USERNAME
        password = System.env.DOCKER_PASSWORD
    }
}

task buildDockerImage(type: DockerBuildImage) {
    description = "Build docker image."
    inputDir = project.rootDir
    images.add("sogis/ilivalidator-web-service:"+version.toString())
    images.add("sogis/ilivalidator-web-service:latest")
    labels = ["ilivalidator-web-service.created": new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()), 
            "ilivalidator-web-service.git_commit": getCheckedOutGitCommitHash(), 
            "ilivalidator-web-service.build": version.toString()]
}

task createDockerContainer(dependsOn: 'buildDockerImage', type: DockerCreateContainer) {
    description = "Create docker container."
    targetImageId buildDockerImage.getImageId() 
    hostConfig.portBindings = ['8888:8888']
    exposedPorts = [new ExposedPort("tcp", [8888])]
}

task startDockerContainer(dependsOn: 'createDockerContainer', type: DockerStartContainer) { 
    description = "Start docker container."
    targetContainerId createDockerContainer.getContainerId() 

    // Starting container != web service is ready.
    doLast { 
        sleep(10*1000)
    }
}

task stopDockerContainer(type: DockerStopContainer) {
    description = "Stop running docker container."
    targetContainerId createDockerContainer.getContainerId()
}

/*
def tags = ["latest", version]

tags.each { tag ->
    def imageTag = tag.toString()
    task "pushDockerImage_$imageTag"(type: DockerPushImage) {
        description = "Push docker image to hub.docker.com: $imageTag"
        imageName =  "sogis/ilivalidator-web-service:$imageTag"
    }
}

task pushDockerImages() {
    description = "Push docker images to hub.docker.com."
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('pushDockerImage_') }
    }    
}
*/

dockerTest.dependsOn('startDockerContainer')
dockerTest.finalizedBy('stopDockerContainer')

def getCheckedOutGitCommitHash() {
    'git log -1 --pretty=%H'.execute().text.trim()
}

task versionTxt()  {
    outputs.upToDateWhen { false }
    doLast {
        new File("version.txt").text = """
Version: $version
Revision: ${getCheckedOutGitCommitHash()}
Buildtime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
Application-name: ilivalidator-web-service
"""
    }
}
task copyVersionTxt(type: Copy) {
    from "version.txt"
    into "src/main/resources/templates/"
}
copyVersionTxt.dependsOn(versionTxt)
build.dependsOn(copyVersionTxt)
classes.dependsOn(copyVersionTxt)
